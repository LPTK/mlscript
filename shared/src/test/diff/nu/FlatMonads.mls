:NewDefs


declare fun String: anything -> Str
//│ fun String: anything -> Str


abstract class IO[A] {
  fun bind(f) = Bind(this, f)
  fun run: A
}
class Pure[A](value: A) extends IO[A] {
  fun run = value
}
class Bind[A, B](underlying: IO[A], f: A -> IO[B]) extends IO[B] {
  fun run = f(underlying.run).run
}
//│ abstract class IO[A] {
//│   fun bind: forall 'A 'B. ('A -> IO['B]) -> Bind['A, 'B]
//│   fun run: A
//│ }
//│ class Pure[A](value: A) extends IO {
//│   fun bind: forall 'A0 'B0. ('A0 -> IO['B0]) -> Bind['A0, 'B0]
//│   fun run: A
//│ }
//│ class Bind[A, B](underlying: IO[A], f: A -> IO[B]) extends IO {
//│   fun bind: forall 'A1 'B1. ('A1 -> IO['B1]) -> Bind['A1, 'B1]
//│   fun run: B
//│ }

module readInt extends IO[Int] { fun run: Int = 42 }
class printLine(str: Str) extends IO[undefined] { fun run = log(str) }
//│ module readInt extends IO {
//│   fun bind: forall 'B 'A. ('A -> IO['B]) -> Bind['A, 'B]
//│   fun run: Int
//│ }
//│ class printLine(str: Str) extends IO {
//│   fun bind: forall 'A0 'B0. ('A0 -> IO['B0]) -> Bind['A0, 'B0]
//│   fun run: ()
//│ }


// * Nested indent:

val main =
  printLine("Hi! Input two numbers: ").bind of _ =>
    readInt.bind of n =>
      readInt.bind of m =>
        val sum = n + m
        printLine(concat("The sum is: ")(String of sum)).bind of _ =>
          Pure(sum)
//│ val main: Bind['A, 'B]
//│   where
//│     'B :> Int
//│ main
//│      = Bind {}

main.run
//│ Int
//│ res
//│     = 84
//│ // Output
//│ Hi! Input two numbers: 
//│ The sum is: 84


// * Flat indent:

val main =
  printLine("Hi! Input two numbers: ").bind of _ =>
  readInt.bind of n =>
  readInt.bind of m =>
  val sum = n + m
  printLine(concat("The sum is: ")(String of sum)).bind of _ =>
  Pure(sum)
//│ val main: Bind['A, 'B]
//│   where
//│     'B :> Int
//│ main
//│      = Bind {}

main.run
//│ Int
//│ res
//│     = 84
//│ // Output
//│ Hi! Input two numbers: 
//│ The sum is: 84


// * TODO improve this error – missing provenance for '0-element tuple'
:e
printLine("").bind of [] => error
//│ Bind['A, 'B]
//│   where
//│     'A <: []
//│ res
//│     = Bind {}
//│ TEST CASE FAILURE: There was an unexpected lack of type error

// * TODO improve this error (parameter list repr.)
:e
printLine("").bind of () => error
//│ ╔══[ERROR] Type mismatch in application:
//│ ║  l.103: 	printLine("").bind of () => error
//│ ║         	^^^^^^^^^^^^^^^^^^^^^^^^
//│ ╟── type `[?A]` does not match type `[]`
//│ ║  l.15: 	class Bind[A, B](underlying: IO[A], f: A -> IO[B]) extends IO[B] {
//│ ╙──      	                                       ^
//│ Bind['A, 'B] | error
//│ res
//│     = Bind {}

printLine("").bind of (()) => error
//│ Bind['A, 'B]
//│   where
//│     'A <: ()
//│ res
//│     = Bind {}


// * Using a shortand operator for `bind`... What's the use of a `do` notation?! :^)

fun (#>>) bind[A, B](x: IO[A], f: A -> IO[B]): IO[B] = x.bind(f)
//│ fun (#>>) bind: forall 'A 'B. (x: IO['A], f: 'A -> IO['B]) -> IO['B]

val main =
  printLine("Hi! Input two numbers: ") #>> _ =>
  readInt #>> n =>
  readInt #>> m =>
  val sum = n + m
  printLine(concat("The sum is: ")(String of sum)) #>> _ =>
  Pure(sum)
//│ val main: IO['B]
//│   where
//│     'B :> Int
//│ main
//│      = Bind {}

main.run
//│ Int
//│ res
//│     = 84
//│ // Output
//│ Hi! Input two numbers: 
//│ The sum is: 84

fun loop =
  printLine("Input a positive number: ") #>> _ =>
  readInt #>> n =>
  if n < 0 then loop else Pure(n)
//│ fun loop: forall 'B. IO['B]
//│   where
//│     'B :> Int

let r = loop.run
//│ let r: Int
//│ r
//│   = 42
//│ // Output
//│ Input a positive number: 


// * Using another shortand operator for `map`

fun (>>) compose[A, B, C](f: A -> B, g: B -> C): A -> C = x => g(f(x))
fun (#>) map[A, B](x: IO[A], f: A -> B): IO[B] = x.bind(f >> Pure)
//│ fun (>>) compose: forall 'B 'C 'A. (f: 'A -> 'B, g: 'B -> 'C) -> 'A -> 'C
//│ fun (#>) map: forall 'A0 'B0. (x: IO['A0], f: 'A0 -> 'B0) -> IO['B0]

val main =
  printLine("Hi! Input two numbers: ") #>> _ =>
  readInt #>> n =>
  readInt #>> m =>
  val sum = n + m
  printLine(concat("The sum is: ")(String of sum)) #> _ =>
  sum
//│ val main: IO['B]
//│   where
//│     'B :> Int
//│ main
//│      = Bind {}


// * With no type annotations:

fun (>>) compose(f, g) = x => g(f(x))
fun (#>>) bind(x, f) = x.bind(f)
fun (#>) map(x, f) = x.bind(f >> Pure)
//│ fun (>>) compose: forall 'a 'b 'c. ('a -> 'b, 'b -> 'c) -> 'a -> 'c
//│ fun (#>>) bind: forall 'd 'e. ({bind: 'd -> 'e}, 'd) -> 'e
//│ fun (#>) map: forall 'f 'A 'g. ({bind: ('f -> Pure['A]) -> 'g}, 'f -> 'A) -> 'g

val main =
  printLine("Hi! Input two numbers: ") #>> _ =>
  readInt #>> n =>
  readInt #>> m =>
  val sum = n + m
  printLine(concat("The sum is: ")(String of sum)) #> _ =>
  sum
//│ val main: Bind['A, 'B]
//│   where
//│     'B :> Int
//│ main
//│      = Bind {}

main.run
//│ Int
//│ res
//│     = 84
//│ // Output
//│ Hi! Input two numbers: 
//│ The sum is: 84

fun loop =
  printLine("Input a positive number: ") #>> _ =>
  readInt #>> n =>
  if n < 0 then loop else Pure(n)
//│ fun loop: forall 'A 'B. Bind['A, 'B]

let r = loop.run
//│ let r: nothing
//│ r
//│   = 42
//│ // Output
//│ Input a positive number: 


// * Abstracting over the monad:

fun main(ctx) =
  ctx.printLine("Hi! Input two numbers: ") #>> _ =>
  ctx.readInt #>> n =>
  ctx.readInt #>> m =>
  val sum = n + m
  ctx.printLine(concat("The sum is: ")(String of sum)) #>> _ =>
  ctx.pure(sum)
//│ fun main: forall 'a 'b 'c 'd 'e. {
//│   printLine: "Hi! Input two numbers: " -> {bind: (anything -> 'c) -> 'b} & Str -> {bind: (anything -> 'e) -> 'd},
//│   pure: Int -> 'e,
//│   readInt: {bind: (Int -> 'a) -> 'c & (Int -> 'd) -> 'a}
//│ } -> 'b

val defaultCtx = {printLine, readInt, pure: Pure}
//│ val defaultCtx: {
//│   printLine: (str: Str) -> printLine,
//│   pure: forall 'A. (value: 'A) -> Pure['A],
//│   readInt: readInt
//│ }
//│ defaultCtx
//│            = {
//│                printLine: [Function (anonymous)] {
//│                  class: [class printLine extends IO],
//│                  unapply: [Function: unapply]
//│                },
//│                readInt: readInt { class: [class readInt extends IO] },
//│                pure: [Function (anonymous)] {
//│                  class: [class Pure extends IO],
//│                  unapply: [Function: unapply]
//│                }
//│              }

main(defaultCtx).run
//│ Int
//│ res
//│     = 84
//│ // Output
//│ Hi! Input two numbers: 
//│ The sum is: 84

fun loop(ctx) =
  ctx.printLine("Input a positive number: ") #>> _ =>
  ctx.readInt #>> n =>
  if n < 0 then loop(ctx) else ctx.pure(n)
//│ fun loop: forall 'a 'b 'c 'd. {
//│   printLine: "Input a positive number: " -> {bind: (anything -> 'a) -> 'b},
//│   pure: 'c -> 'd,
//│   readInt: {bind: ((Num & 'c) -> ('d | 'b)) -> 'a}
//│ } -> 'b

let r = loop(defaultCtx)
//│ let r: Bind['A, 'B]
//│ r
//│   = Bind {}

let r = loop(defaultCtx).run
//│ let r: nothing
//│ r
//│   = 42
//│ // Output
//│ Input a positive number: 

:e
not(r)
//│ Bool
//│ res
//│     = false
//│ TEST CASE FAILURE: There was an unexpected lack of type error


// * Note: using inferred parent type arguments

module readInt extends IO { fun run = 42 }
class printLine(str: Str) extends IO { fun run = log(str) }
//│ module readInt extends IO {
//│   fun bind: forall 'A 'B. ('A -> IO['B]) -> Bind['A, 'B]
//│   fun run: 42
//│ }
//│ class printLine(str: Str) extends IO {
//│   fun bind: forall 'B0 'A0. ('A0 -> IO['B0]) -> Bind['A0, 'B0]
//│   fun run: ()
//│ }

val main =
  printLine("Hi! Input two numbers: ").bind of _ =>
  readInt.bind of n =>
  readInt.bind of m =>
  val sum = n + m
  printLine(concat("The sum is: ")(String of sum)).bind of _ =>
  Pure(sum)
//│ val main: Bind['A, 'B]
//│   where
//│     'B :> Int
//│ main
//│      = Bind {}

main
//│ Bind['A, 'B]
//│   where
//│     'B :> Int
//│ res
//│     = Bind {}


:e
let r = printLine("").bind of 0 => Pure(1)
//│ let r: Bind['A, 'B]
//│   where
//│     'B :> 1
//│     'A <: 0
//│ r
//│   = Bind {}
//│ TEST CASE FAILURE: There was an unexpected lack of type error

:e
let r = printLine("").bind of x =>
log(x.a)
Pure(1)
//│ let r: Bind['A, 'B]
//│   where
//│     'B :> 1
//│     'A <: {a: anything}
//│ r
//│   = Bind {}
//│ TEST CASE FAILURE: There was an unexpected lack of type error

:re
r.run
//│ 1
//│ res
//│ Runtime error:
//│   TypeError: Cannot read properties of undefined (reading 'a')


// * We can even technically support the following syntax...

:NoJS // * TODO We'll need to support functions extended with fields

// * An interface that describes monadic functions with a field `run`
declare trait IO[A]: (forall 'b: (A -> IO['b]) -> IO['b]) {
  fun run: A
}
declare fun pure: 'a -> IO['a]
declare fun readInt: IO[Int]
declare fun printLine: Str -> IO[()]
//│ declare trait IO[A]: forall 'b. (A -> IO['b]) -> IO['b] {
//│   fun run: A
//│ }
//│ fun printLine: Str -> IO[()]
//│ fun pure: forall 'a. 'a -> IO['a]
//│ fun readInt: IO[Int]

val main =
  printLine("Hi! Input two numbers: ") of _ =>
  readInt of n =>
  readInt of m =>
  val sum = n + m
  printLine(concat("The sum is: ")(String of sum)) of _ =>
  pure(sum)
//│ val main: IO['b]
//│   where
//│     'b :> Int

main.run
//│ Int


